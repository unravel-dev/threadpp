cmake_minimum_required(VERSION 3.14)

set(THREADPP_MAIN_PROJECT ON)
if(DEFINED PROJECT_SOURCE_DIR)
    set(THREADPP_MAIN_PROJECT OFF)
endif()

project(threadpp)
include(CTest)

set(CMAKE_MODULE_PATH ${CMAKE_MODULE_PATH} "${CMAKE_CURRENT_SOURCE_DIR}/cmake")
set(CMAKE_MODULE_PATH ${CMAKE_MODULE_PATH} "${CMAKE_CURRENT_SOURCE_DIR}/cmake/utility")

option(BUILD_THREADPP_SHARED "Build as a shared library." ${THREADPP_MAIN_PROJECT})
option(BUILD_THREADPP_TESTS "Build the tests" ${THREADPP_MAIN_PROJECT})
option(BUILD_THREADPP_WITH_CODE_STYLE_CHECKS "Build with code style checks." ${THREADPP_MAIN_PROJECT})

if(BUILD_THREADPP_TESTS)
	if(NOT CMAKE_RUNTIME_OUTPUT_DIRECTORY)
		set(CMAKE_RUNTIME_OUTPUT_DIRECTORY ${PROJECT_BINARY_DIR}/bin)
	endif()
	if(NOT CMAKE_ARCHIVE_OUTPUT_DIRECTORY)
		set(CMAKE_ARCHIVE_OUTPUT_DIRECTORY ${PROJECT_BINARY_DIR}/lib)
	endif()
	if(NOT CMAKE_LIBRARY_OUTPUT_DIRECTORY)
		set(CMAKE_LIBRARY_OUTPUT_DIRECTORY ${PROJECT_BINARY_DIR}/lib)
	endif()
endif()

if(BUILD_THREADPP_SHARED)
	set(BUILD_SHARED_LIBS ON)
endif()

if(BUILD_THREADPP_WITH_CODE_STYLE_CHECKS)
	set(BUILD_WITH_CODE_STYLE_CHECKS ON)
endif()

add_subdirectory(threadpp)

if(BUILD_THREADPP_TESTS)
	add_subdirectory(tests)
endif()
